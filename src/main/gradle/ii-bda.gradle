/**
 * Copyright 2010-2016 interactive instruments GmbH
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import java.util.regex.Matcher

/**
 * etf build and deployment helper for gradle
 *
 * @author J. Herrmann ( herrmann <aT) interactive-instruments (doT> de )
 */
println 'ETF-BDA: Applying ii BDA gradle script to the project'

buildscript {
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        // Release plugin
        classpath 'net.researchgate:gradle-release:2.3.5'
        // Code formatting and license headers plugin
        classpath 'com.diffplug.gradle.spotless:spotless:1.3.2'
        // Git plugin (to get revision number)
        classpath 'org.ajoberstar:grgit:1.1.0'
    }
}

ext.versionFile = rootProject.file("./version.properties")

// Set and check project version
if (!ext.versionFile.exists()) {
    throw new GradleException("Required file version.properties does not exist! Create it!")
}
if (!ext.versionFile.canRead()) {
    throw new GradleException("Can not read file version.properties ! Check file permissions!")
}
def versionProperties = new Properties()
ext.versionFile.withInputStream {
    stream -> versionProperties.load(stream)
}
if (versionProperties.version == null) {
    throw new GradleException("Property version not set in file version.properties!")
}

if (hasProperty("teamcity")) {
    ext.versionBuildNumber = teamcity["build.number"]
} else if (System.getenv("BUILD_NUMBER")) {
    ext.versionBuildNumber = System.getenv("BUILD_NUMBER")
} else {
    ext.versionBuildNumber = '0'
}

def sTasks = project.gradle.startParameter.taskNames
if (versionProperties.version.toLowerCase().contains("snapshot")) {
    if (sTasks.contains('release') || sTasks.contains('runBuildTasks')) {
        // First case applies when you call release taks, second one is one of
        // the tasks that are executed in a NEW gradle instance
        println "ETF-BDA: Ignoring SNAPSHOT handling during RELEASE task execution."
        ext.snapshotSuffix = ""
    } else {
        ext.snapshotSuffix = "-SNAPSHOT"
    }
} else {
    ext.snapshotSuffix = ""
}

// Check credentials
if (!project.hasProperty("ii.etfdev.repo.username")) {
    throw new GradleException("Property ii.etfdev.repo.username not set!")
}
if (!project.hasProperty("ii.etfdev.repo.password")) {
    throw new GradleException("Property ii.etfdev.repo.password not set!")
}
ext.r_user = project.getProperty("ii.etfdev.repo.username")
ext.r_pwd = project.getProperty("ii.etfdev.repo.password")

println "ETF-BDA: Authenticating will be done as user ${ext.r_user}"

ext.qualityReports = project.hasProperty("etf.dev.quality.reports") &&
        project.getProperty("etf.dev.quality.reports") == "true"

allprojects {

    // Default lib versions
    ext {
      // testCompile group: 'junit', name: 'junit', version: junitVersion
      junitVersion = '4.11'

      // compile group: 'org.slf4j', name: 'slf4j-api', version: slf4jApiVersion
      slf4jApiVersion = '1.7.12'

      // compile group: 'org.apache.commons', name: 'commons-lang3', version: commonsLang3Version
      commonsLang3Version = '3.4'

      // compile group: 'commons-codec', name: 'commons-codec', version: commonsCodecVersion
      commonsCodecVersion = '1.10'

      // compile group: 'commons-io', name: 'commons-io', version: commonsIoVersion
      commonsIoVersion = '2.4'

      // compile group: 'xerces', name: 'xercesImpl', version: xercesVersion
      xercesVersion = '2.11.0.beta'

      // compile group: 'xml-apis', name: 'xml-apis', version: xmlApisVersion
      xmlApisVersion = '1.4.01'

      // compile group: 'commons-collections', name: 'commons-collections', version: commonsCollectionsVersion
      commonsCollectionsVersion = '3.2.1'

      // compile group: 'org.glassfish.jaxb', name: 'jaxb-runtime', version: jaxbVersion
      jaxbVersion = '2.2.11'
    }

    apply plugin: 'java'
    apply plugin: 'maven'

    version = versionProperties.version
    assert version != null

    ext.snapshotSuffix = gradle.rootProject.ext.snapshotSuffix
    assert ext.snapshotSuffix != null

    ext.versionBuildNumber = gradle.rootProject.ext.versionBuildNumber
    assert ext.versionBuildNumber != null

    ext.repo = org.ajoberstar.grgit.Grgit.open(file(project.rootDir))
    assert ext.repo != null
    ext.repoUrl = ext.repo.remote.list()[0].url

    assert gradle.rootProject.ext.r_user != null
    assert gradle.rootProject.ext.r_pwd != null

    ext.spotlessJavaSrcFiles = project.fileTree(project.projectDir) {
        include 'src/main/java/de/interactive_instruments/**/*.java'
        include 'src/test/java/de/interactive_instruments/**/*.java'
        exclude '**/package-info.java'
        exclude 'apache2-license-header.java'
    }

    repositories {
        // First get modified external libraries
        maven {
            url "http://services.interactive-instruments.de/etfdev-af/ext-releases-local"
            credentials {
                username gradle.rootProject.ext.r_user
                password gradle.rootProject.ext.r_pwd
            }
        }
        maven {
            url "http://services.interactive-instruments.de/etfdev-af/ext-cache"
            credentials {
                username gradle.rootProject.ext.r_user
                password gradle.rootProject.ext.r_pwd
            }
        }
        mavenCentral()
        if (project.snapshotSuffix) {
            println "ETF-BDA: Activating local repository for SNAPSHOT version"
            mavenLocal()

            maven {
                url "http://services.interactive-instruments.de/etfdev-af/snapshot"
                credentials {
                    username gradle.rootProject.ext.r_user
                    password gradle.rootProject.ext.r_pwd
                }
            }
        } else {
            maven {
                url "http://services.interactive-instruments.de/etfdev-af/release"
                credentials {
                    username gradle.rootProject.ext.r_user
                    password gradle.rootProject.ext.r_pwd
                }
            }
        }
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    if (gradle.rootProject.ext.qualityReports) {
        /////////////////////////////////////
        // STATIC CODE ANALYSIS - FINDBUGS //
        /////////////////////////////////////
        apply plugin: 'findbugs'
        findbugs {
            toolVersion = "3.0.1"
            ignoreFailures = true
            reportsDir = file("$project.buildDir/reports/findbugs")
            effort = "max"
            reportLevel = "high"
        }
        findbugsMain {
            reports {
                xml.enabled = false
                html.enabled = true
            }
        }

        ////////////////////////////////
        // STATIC CODE ANALYSIS - PMD //
        ////////////////////////////////
        apply plugin: 'pmd'
        pmd {
            ignoreFailures = true
            sourceSets = [sourceSets.main]
            reportsDir = file("$project.buildDir/reports/pmd")
        }
        pmdMain {
            reports {
                xml.enabled = false
                html.enabled = true
            }
        }

        ////////////////////////////////////
        // CODE COVERAGE METRICS - JACOCO //
        ////////////////////////////////////
        apply plugin: 'jacoco'
        jacoco {
            reportsDir = file("$project.buildDir/reports/jacoco")
        }
        jacocoTestReport {
            reports {
                xml.enabled = false
                html.enabled = true
            }
        }

        //////////////////////////////////////
        // DESIGN QUALITY METRICS - JDEPEND //
        //////////////////////////////////////
        apply plugin: 'jdepend'
        jdepend {
            reportsDir = file("$project.buildDir/reports/jdepend")
        }
        jdependMain {
            reports {
                xml.enabled false
                text.enabled true
            }
        }


        ////////////////////
        // PROJECT-REPORT //
        ////////////////////
        apply plugin: 'project-report'

    }

    ////////////
    // UPLOAD //
    ////////////
    uploadArchives {
        repositories {
            mavenDeployer {
                snapshotRepository(url: "http://services.interactive-instruments.de/etfdev-af/snapshot") {
                    authentication(userName: gradle.rootProject.ext.r_user, password: gradle.rootProject.ext.r_pwd)
                }
                repository(url: "http://services.interactive-instruments.de/etfdev-af/release") {
                    authentication(userName: gradle.rootProject.ext.r_user, password: gradle.rootProject.ext.r_pwd)
                }

                pom.project {
                    url 'http://interactive-instruments.github.io/etf-webapp'

                    scm {
                        url = "${project.repoUrl}"
                        connection = "${project.repoUrl}"
                        developerConnection = "${project.repoUrl}"
                    }

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id = 'jonherrmann'
                            organization = 'interactive instruments'
                        }
                        developer {
                            id = 'cportele'
                            organization = 'interactive instruments'
                        }
                        developer {
                            id = 'jechterhoff'
                            organization = 'interactive instruments'
                        }
                    }

                    properties {
                        'project.build.sourceEncoding' 'UTF-8'
                        'project.reporting.outputEncoding' 'UTF-8'
                    }
                }
            }
        }
    }

    //////////////
    // MANIFEST //
    //////////////
    jar.doFirst {
        manifest {
            attributes(
                    'Implementation-Title': project.name,
                    'Specification-Vendor': 'interactive instruments GmbH',
                    'Specification-Vendor-Id': 'de.interactive_instruments',
                    'Implementation-Vendor': 'interactive instruments GmbH',
                    'Implementation-Vendor-Id': 'de.interactive_instruments',
                    'Implementation-Version': project.version,
                    'Implementation-Revision': repo.head().abbreviatedId,
                    'Implementation-Build': project.versionBuildNumber,
                    'Built-By': gradle.rootProject.ext.r_user,
                    'Build-User': System.getProperty('user.name'),
                    'Build-Host': java.net.InetAddress.getLocalHost().getHostName(),
                    'Build-JDK': System.getProperty('java.version'),
                    'Build-Date': new Date().format('yyMMddHHmmss'),
                    'Build-Gradle': gradle.gradleVersion,
                    'Source-Compatibility': project.sourceCompatibility,
                    'Target-Compatibility': project.targetCompatibility,
            )
        }
    }

    ////////////////////////////////////////
    // CODE FORMATTING AND LICENSE HEADER //
    ////////////////////////////////////////
    apply plugin: com.diffplug.gradle.spotless.SpotlessPlugin
    spotless {
        java {
            target spotlessJavaSrcFiles
            licenseHeaderFile rootProject.file('./build/gradle/apache2-license-header.java')
            importOrder(['java', 'javax', 'com', 'org', 'de'])
            eclipseFormatFile rootProject.file('./build/gradle/etf.eclipseformat.xml')
            trimTrailingWhitespace()
            // endWithNewline()

            custom 'Lambda fix', { it.replace('} )', '})').replace('} ,', '},') }
        }

        format 'groovy', {
            target '**/*.groovy'
            licenseHeaderFile rootProject.file('./build/gradle/apache2-license-header.java'), "package "
        }

        format 'misc', {
            target '**/*.md', '**/*.gitignore'
            indentWithTabs()
            trimTrailingWhitespace()
            endWithNewline()
        }
    }

    ///////////////
    // RELEASING //
    ///////////////
    apply plugin: net.researchgate.release.ReleasePlugin
    release {
        ext.snapshotSuffix = ""
        failOnCommitNeeded = true
        failOnPublishNeeded = true
        failOnSnapshotDependencies = true
        failOnUnversionedFiles = true
        failOnUpdateNeeded = true
        revertOnFail = true
        preCommitText = ''
        preTagCommitMessage = '[RELEASE] :new: Version '
        tagCommitMessage = 'Version '
        newVersionCommitMessage = 'New working version '
        tagTemplate = '${version}'
        versionPropertyFile = 'version.properties'
        versionProperties = []
        buildTasks = ['build']
        versionPatterns = [
                /(\d+)([^\d]*$)/: { Matcher m, Project p -> m.replaceAll("${(m[0][1] as int) + 1}${m[0][2]}") }
        ]

        git {
            requireBranch = 'master'
            pushToRemote = 'origin'
        }
    }

    if (tasks.findByPath('uploadArchives') != null) {
        afterReleaseBuild.dependsOn uploadArchives
    }

    install.doLast {
        println "ETF-BDA: Installed version ${version}"
    }

    uploadArchives.doLast {
        println "ETF-BDA: Uploaded version ${version}"
    }
}
